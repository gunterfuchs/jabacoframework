<?xml version="1.0" encoding="UTF-8"?>
<SourceFile><Source><![CDATA[' {CheckBoxMenuItem wrapper for jabaco}
' @since 2010-07-30
' @author Alarcón Franco Andrés

Option Explicit

Implements VB#IJabacoClass 
Implements VB#IMenuItem 

Dim myIconPath As #IResource
Dim myControlID As String 
Dim myMenuBar As VBMenuBar

Public Function getBackground() As #Color
   If myMenuBar = Nothing Then
      getBackground = Base.getBackground()
      Call Parent.setOpaque(True)
   Else
      getBackground = myMenuBar.getBackground()
      Call Parent.setOpaque(True)
   End If
End Function

Public Function getForeground() As #Color
   If myMenuBar = Nothing Then
      getForeground = Base.getForeground()
   Else
      getForeground = myMenuBar.getForeground()
   End If
End Function

Public Function getFont() As #Font 
   If myMenuBar = Nothing Then
      getFont = Base.getFont()
   Else
      getFont = myMenuBar.getFont()
   End If
End Function

Public Sub VBCheckBoxMenuItem(refMenuBar As VBMenuBar, refOwner As #JComponent, _
   refControlID As String, refCaption As String, refChecked As Boolean, _
   refEnabled As Boolean, refVisible As Boolean, refIconPath As #IResource)

   myMenuBar = refMenuBar
   If refOwner = Null Then
      Call refMenuBar.add(Me)
   Else
      Call refOwner.add(Me)
   End If

   Call Parent.addActionListener(refMenuBar)
   Caption = refCaption
   ControlID = refControlID
   Enabled = refEnabled
   Visible = refVisible
   Icon = refIconPath
   
   Base.setState(refChecked)
   
   'If refOwner <> Nothing Then 
   '   If Parent.getIcon() = Nothing Then
   '      Parent.setIcon CreateIcon(16, 16)
   '   End If
   'End If

End Sub

Public Property Get ControlID() As String
    ControlID = myControlID
End Property

Public Property Let ControlID(s As String)
    myControlID = s
End Property

Public Property Get Caption() As String
    Caption = Me.getLabel()
End Property

Public Property Let Caption(s As java#lang#String)
   Dim mnemonicPos As Integer
   mnemonicPos = s.indexOf("&")
   If ((mnemonicPos > -1) And (mnemonicPos < s.length())) Then   
      Me.setMnemonic(s.charAt(mnemonicPos+1))      
   End If
   Me.setLabel(s.replaceFirst("&", ""))
End Property

Public Property Get Icon() As #IResource
   Icon = myIconPath
End Property

Public Property Let Icon(Path As #IResource)
   On Error Goto ERROUT
   If Path <> Nothing Then
      If Len(Path) > 0 Then 
         Me.setIcon LoadIcon(Path)
         myIconPath = Path
      End If
   End If
ERROUT:
End Property

Public Property Get Checked() As Boolean
   Checked = Base.getState()
End Property

Public Property Let Checked(v As Boolean)
   Base.setState(v)
End Property

Public Property Get Enabled() As Boolean 
   Enabled = Me.isEnabled()
End Property

Public Property Let Enabled(v As Boolean)
   Me.setEnabled v
End Property

Public Property Get Visible() As Boolean 
   Visible = Me.isVisible()
End Property

Public Property Let Visible(v As Boolean)
   Me.setVisible v
End Property

Public Property Get Parent() As #JMenuItem 
   Parent = Base
End Property


























]]></Source><Param Name="(Name)" Value="VBCheckBoxMenuItem"/><Param Name="(SuperClass)" Value="javax/swing/JCheckBoxMenuItem"/><Param Name="(NameSpace)" Value="VB"/><Param Name="(Type)" Value="Class"/><Param Name="(Access)" Value="Public"/></SourceFile>
